package org.drools.planner.examples.tournaments;

import org.drools.planner.core.score.calculator.HardAndSoftConstraintScoreCalculator;
import org.drools.planner.examples.tournaments.model.Match;
import org.drools.planner.examples.tournaments.model.Team;

import function org.drools.planner.examples.tournaments.Util.areTeamsShared
import function org.drools.planner.examples.tournaments.Util.areSlotsShared

global HardAndSoftConstraintScoreCalculator scoreCalculator;

declare Overhead
    num: Integer
end

declare Distance
end

rule "Get underruns per team"
    when
        $m1: Match()
        $m2: Match(this != $m1, areTeamsShared($m1.teams, this.teams) || areSlotsShared($m1, this))
    then
        int distance = Math.abs($m1.getSlot().getNumber() - $m2.getSlot().getNumber());
        if (distance < 2) {
            // if inside a rule; ugly, but performs much faster then filtering in the later accumulate
            insertLogical(
                new Distance()
            );
        }
end

rule "Get overhead per team"
    when
        $t: Team()
        accumulate(
            Match($m: this, teams contains $t),
            $min: min($m.getSlot().getNumber()),
            $max: max($m.getSlot().getNumber()),
            $count: count($m.getSlot().getNumber())
        )
    then
        insertLogical(
            new Overhead($max.intValue() - $min.intValue() - (($count.intValue() * 2) - 1))
        );
end

rule "Hard score means how many teams have matches directly adjacent"
    when
        $total : Number () from accumulate(
            Distance(),
            count()
        )
    then
        scoreCalculator.setHardConstraintsBroken($total.intValue());
end

rule "Soft score is a function of max slot distance and their sum"
    when
        accumulate(
            Overhead($overhead : num, num > 0),
            $max: max($overhead),       
            $sum: sum($overhead)
        )
    then
        scoreCalculator.setSoftConstraintsBroken($max.intValue() * $sum.intValue());
end