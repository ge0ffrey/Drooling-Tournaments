package org.drools.planner.examples.tournaments;

import org.drools.planner.core.score.calculator.HardAndSoftConstraintScoreCalculator;
import org.drools.planner.examples.tournaments.model.Match;
import org.drools.planner.examples.tournaments.model.Team;

import function org.drools.planner.examples.tournaments.Util.areTeamsShared

global HardAndSoftConstraintScoreCalculator scoreCalculator;

declare Overhead
    num: Integer
end

declare InvalidDistance
end

rule "Get underruns per team"
    when
        $m1: Match()
        $m2: Match(this != $m1, areTeamsShared($m1.teams, this.teams))
    then
        int distance = Math.abs($m1.getSlot().getNumber() - $m2.getSlot().getNumber());
        if (distance == 1) {
            insertLogical(
                new InvalidDistance()
            );
        }
end

rule "Get overhead per team"
    when
        $t: Team()
        accumulate(
            Match($m: this, teams contains $t),
            $min: min($m.getSlot().getNumber()),
            $max: max($m.getSlot().getNumber()),
            $count: count($m.getSlot().getNumber())
        )
    then
        insertLogical(
            new Overhead($max.intValue() - $min.intValue() - (($count.intValue() * 2) - 1))
        );
end

rule "Hard score means how many teams have matches directly adjacent"
    when
        $total : Number () from accumulate(
            InvalidDistance(),
            count()
        )
    then
        scoreCalculator.setHardConstraintsBroken($total.intValue());
end

rule "Soft score is a sum of calculated maximum slot distances"
    when
        $total : Number () from accumulate(
            Overhead( $overhead : num, num > 0),
            sum($overhead)
        )
    then
        scoreCalculator.setSoftConstraintsBroken($total.intValue());
end